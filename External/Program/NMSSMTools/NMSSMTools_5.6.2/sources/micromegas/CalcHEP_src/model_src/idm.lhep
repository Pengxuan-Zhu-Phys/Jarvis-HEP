
keys gauge_fixing=Feynman, CKMdim=1, hgg=Off,h4G=Off.

do_if hgg==On.
	do_if h4G==On.
		model  'IDM(+hgg+h4G)'/5.
	do_else.
		model 'IDM(+hgg)'/5.
	end_if.
do_else.
	model 'IDM'/4.
end_if.


read 'sm.inc'.


parameter MHX=111,MH3=222,MHC=333.
parameter laL=0.01, la2=0.01.

parameter mu2=MHX**2-laL*(2*MW/EE*SW)**2.
parameter la3=2*(MHC**2-mu2)/(2*MW/EE*SW)**2.
parameter la5=(MHX**2-MH3**2)/(2*MW/EE*SW)**2.
parameter la4=2*laL-la3-la5.


scalar '~H3'/'~H3':('odd Higgs',pdg 36, mass MH3, width wH3 = auto).
scalar '~H+'/'~H-':('Charged Higgs',pdg 37, mass MHC, width wHC=auto).
scalar '~X'/'~X'  :('second Higgs', pdg 35,  mass MHX, width wHX=auto).



let sId = { -i*'~H+',  ('~X'+i*'~H3')/Sqrt2 },    % scalar inert doublet 
    sID = anti(sId).                              % scalar inert anti-doublet

let DsId^mu^a = (deriv^mu-i*g1/2*B1^mu)*sId^a - i*g2/2*tau^a^b^c*W^mu^c*sId^b.

let DsID^mu^a = (deriv^mu+i*g1/2*B1^mu)*sID^a + i*g2/2*tau^b^a^c*W^mu^c*sID^b.



lterm DsID*DsId.



lterm -mu2*sId*sID.


lterm -la2*(sId*sID)**2.
lterm -la3*(shd*shD)*(sId*sID).
lterm -la4*(shd*sID)*(shD*sId).
lterm -la5/2*(shd*sID)**2 + AddHermConj.




CheckHerm.

CheckMasses.
